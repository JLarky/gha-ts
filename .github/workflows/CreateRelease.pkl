amends "package://pkg.pkl-lang.org/github.com/stefma/pkl-gha/com.github.action@0.0.1#/GitHubAction.pkl"

name = "CreateRelease"

on {
  push {
    tags {
      "*"
    }
  }
}

permissions = new Permissions {
  contents = "write"
}

jobs {
  ["createRelease"] {
    `runs-on` = new UbuntuLatest {}
    steps {
      new UsesStep {
        name = "checkout"
        uses = "actions/checkout@v4"
      }
      new RunStep {
        name = "Install pkl cli and add to path"
        run = """
          curl -L -o pkl https://github.com/apple/pkl/releases/download/0.25.2/pkl-linux-amd64
          chmod +x pkl
          ./pkl --version

          mkdir -p $HOME/.local/bin
          mv pkl $HOME/.local/bin/pkl
          echo "$HOME/.local/bin" >> $GITHUB_PATH
          """
      }
      new RunStep {
        name = "Package pkl module"
        run = "pkl project package"
        env {
          ["VERSION"] = "${{ github.ref_name }}"
        }
      }
      new RunStep {
        name = "Extract version number from tag"
        run = """
          VERSION_NUMBER=${{ github.ref_name }}
          VERSION_NUMBER=${VERSION_NUMBER#*@}
          echo "VERSION_NUMBER=$VERSION_NUMBER" >> $GITHUB_ENV
          """
      }
      new UsesStep {
        uses = "softprops/action-gh-release@v1"
        with {
          ["files"] = ".out/com.github.action@${{ env.VERSION_NUMBER }}/*"
          ["fail_on_unmatched_files"] = "true"
          ["prerelease"] = "true"
          ["draft"] = "true"
        }
      }
    }
  }
}